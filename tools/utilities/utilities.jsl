/*""" Addin creation template

Author: jthi
Creation Date: YYYY-MM-DD
Creation JMP Version: JMP Pro 17.2

Description: Longer description

Todo:
    * 
	
"""*/

Names Default To Here(1);

mini_modal_window = function({title, text, text_icon = "BlankIndex", window_icon = "NewApplication"}, {Default Local},
	/*""" Show simple modal window to user
	
	Args:
		title (str): Title of the modal window
		text (str): Text to display to user
		text_icon (str): Icon displayed next to text
		window_icon (str): Window icon
	
	Returns:
		num: 1 if user pressed OK and 0 if user pressed cancel / cross
	Raises:
		exception_msg
	"""*/
	nw = New Window(title, <<modal, << return result, << show toolbars(0), << show menu(0), << Set Window Icon(window_icon),
		H List Box(
			Panel Box("Message",
				H List Box(
					Icon Box(text_icon), 
					Spacer Box(size(10, 1)), 
					V Center Box(Text Box(text, << Set Wrap(300)))
				)
			),
			Spacer Box(Size(10, 0)),
			Panel Box("Actions",
				Lineup Box(N Col(1),
					Button Box("OK"),
					Button Box("Cancel")
				)
			)
		)
	);
	return(nw["Button"]);
);


move_window = function({nw}, {Default Local},

	nw << Show Window(0);
	
	Try(
		cur_window_pos = Current Window() <<Get Window Position();
		cur_window_size = Current Window() <<Get Window Size();
		nw << move window(
			cur_window_pos[1] + cur_window_size[1] / 2 - (nw << Get Window Size)[1] / 2,
			cur_window_pos[2] + cur_window_size[2] / 2 - (nw << Get Window Size)[2] / 2
		);
	);
	nw  << Bring Window To Front;
	nw << Show Window(1);
);

check_data_tables = function({}, {Default Local},
	If(N Items(Get Data Table List()) == 0,
		mini_modal_window("Error", "No datatables open", text_icon = "Error", window_icon = "ErrorSmall");
		stop();
	);
	return(Current Data Table());
);

check_open_window = function({window_title, show_modal = 0}, {Default Local},
	If(Contains(Get Window List() << get window title, window_title),
		If(show_modal,
			mini_modal_window("Window already open", 
				"Window " || window_title || " already open.\!NBringing to front.",
				"WinInformation",
				"Warning"
			);
		);
		move_window(Window(window_title));
		stop();
		write();
	);
);

Write();